apply plugin: 'com.jdroid.android.library'

ext.PROJECT_NAME = 'Jdroid Android - Google Maps'
description = 'Jdroid dependency project for Google Maps'

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

buildscript {
	dependencies {
		classpath "com.jdroidframework:jdroid-gradle-android-plugin:${JDROID_GRADLE_PLUGIN_VERSION}"
	}
}

dependencies {

	debugCompile project(path: ":jdroid-android", configuration: 'debug')
	if (jdroid.isReleaseBuildTypeEnabled()) {
		releaseCompile project(path: ":jdroid-android", configuration: 'release')
	}

	compile "com.google.android.gms:play-services-maps:${GOOGLE_PLAY_SERVICES_VERSION}"

	if (jdroid.getBooleanProp("JDROID_JAVA_HTTP_AS_PROJECT_DEPENDENCY", false)) {
		testCompile project(":jdroid-java-http-okhttp")
	} else {
		testCompile ("com.jdroidframework:jdroid-java-http-okhttp:${JDROID_JAVA_HTTP_VERSION}") {
			exclude module: 'slf4j-api'
			exclude module: 'jdroid-java-core'
		}
	}

	testCompile "junit:junit:${JUNIT_VERSION}"
	testCompile "org.robolectric:robolectric:${ROBOLECTRIC_VERSION}"
}

jdroid {
	notDefaultLanguages = ['es']
}

android {

	defaultConfig {
		manifestPlaceholders = [ googleMapsApiKey: "" ]
	}

	sourceSets.test.java.srcDirs += "build/generated/source/r/debug"
	sourceSets.test.java.srcDirs += sourceSets.main.java.srcDirs
	sourceSets.test.java.srcDirs += sourceSets.debug.java.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.debug.resources.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.main.res.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.debug.res.srcDirs
}
