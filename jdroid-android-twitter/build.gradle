apply plugin: 'com.jdroid.android.library'

ext.PROJECT_NAME = 'Jdroid Android - Twitter SDK'
description = 'Jdroid dependency project for Twitter SDK'

repositories {
	// TODO See how to remove this dependency
	maven {
		url 'https://maven.fabric.io/public'
	}
}

configurations.all {
	// check for updates every build
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

	debugCompile project(path: ":jdroid-android", configuration: 'debug')
	if (jdroid.isReleaseBuildTypeEnabled()) {
		releaseCompile project(path: ":jdroid-android", configuration: 'release')
	}

	// https://docs.fabric.io/android/changelog.html#tweetui
	// https://twittersdk.artifactoryonline.com/twittersdk/public/com/twitter/sdk/android/tweet-ui/
	compile('com.twitter.sdk.android:tweet-ui:2.0.0@aar') {
		transitive = true;
	}

	testCompile 'junit:junit:4.12'
	testCompile "org.mockito:mockito-all:1.10.19"

	// https://github.com/robolectric/robolectric
	testCompile 'org.robolectric:robolectric:3.1'
}

jdroid {
	notDefaultLanguages = ['es']
}

android {

	lintOptions {
		disable 'ContentDescription', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry', 'UseCompoundDrawables', 'UnknownIdInLayout'
	}

	sourceSets.test.java.srcDirs += "build/generated/source/r/debug"
	sourceSets.test.java.srcDirs += sourceSets.main.java.srcDirs
	sourceSets.test.java.srcDirs += sourceSets.debug.java.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs
	sourceSets.test.resources.srcDirs += sourceSets.debug.resources.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.main.res.srcDirs
	sourceSets.test.res.srcDirs += sourceSets.debug.res.srcDirs
}
